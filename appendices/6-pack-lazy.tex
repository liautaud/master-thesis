\section{Simplified implementation of a hybrid pack-file backend.}
\label{app:pack-lazy}

\setminted{fontsize=\footnotesize,baselinestretch=1}
\begin{minted}{ocaml}
module Content_Addressable (V : S.HASHABLE) = struct
  module Index = Index.Make
     (struct type t = V.Digest.t end)  (* Use hash digests as keys. *)
     (struct type t = int64 * int end) (* Use (offset, size) pairs as values. *)

  type t = {
    (* Two atomically switcheable packs for copying garbage collection. *)
    mutable current : [ `A | `B ];
    index_a : Index.t;
    index_b : Index.t;
    pack_a : Pack.t;
    pack_b : Pack.t;
    filter_lock : Lwt_mutex.t;

    (* Mutable set of (offset, size) in the pack that have been reclaimed. *)
    mutable reclaimed : (int64 * int) list;
    mutable reclaimed_count : int64;
    mutable used_pack_size : int64;
    mutable total_pack_size : int64;
  }

  type key = V.Digest.t

  type value = V.t

  (** Updates the value of t.current. *)
  let set_current t c =
    (* In a real implementation, the value of current should also be persisted
       to disk so that it can be retrieved when the backend is first opened. *)
    t.current <- c

  (** Returns the [Pack.t] corresponding with [t.current]. *)
  let get_current_pack t =
    match t.current with `A -> t.pack_a | `B -> t.pack_b

  (** Returns the [Pack.t] corresponding with the opposite of [t.current]. *)
  let get_other_pack t = match t.current with `A -> t.pack_b | `B -> t.pack_a

  (** Returns the [Index.t] corresponding with [t.current]. *)
  let get_current_index t =
    match t.current with `A -> t.index_a | `B -> t.index_b

  (** Returns the [Index.t] corresponding with the opposite of [t.current]. *)
  let get_other_index t =
    match t.current with `A -> t.index_b | `B -> t.index_a

  let mem t k =
    let index = get_current_index t in
    Lwt.return
      ( match Index.find index k with
      (* The offset is -1 when the object has been reclaimed. *)
      | -1L, _ -> false
      (* The object doesn't exist. *)
      | exception Not_found -> false
      | _ -> true )

  let find t k =
    let index = get_current_index t in
    let pack = get_current_pack t in
    let found =
      match Index.find index k with
      | -1L, _ ->
          None (* The offset is -1 when the object has been reclaimed. *)
      | offset, size ->
          let buffer = Bytes.create size in
          let read = Pack.read pack ~off:offset buffer in
          if read < size then None
          else
            Bytes.unsafe_to_string buffer
            |> V.unserialize
            |> Result.to_option
      | exception Not_found -> None
    in
    Lwt.return found

  (** Finds an offset in the pack at which to insert an object of some [size].

      - Starts by looking for space in the list of previously [reclaimed]
        offsets. If such an offset exists, returns [`Offset o]. Note that this
        operation can mutate the [reclaimed] list, e.g. to split a larger block
        of reclaimed memory into two blocks.
      - If no such space is available, returns [`Append] instead. *)
  let allocate t requested_size =
    let first_fits = ref None in
    let find_first_fits (offset, size) =
      if Option.is_some !first_fits then
        Some (offset, size)
      else if size >= requested_size then (
        first_fits := Some offset;
        if size - requested_size > split_threshold then
          Some (offset ++ Int64.of_int requested_size,
                size - requested_size)
        else (
          t.reclaimed_count <- t.reclaimed_count -- 1L;
          None ) )
      else
        Some (offset, size)
    in

    (* Find the first reclaimed region of the pack whose size is sufficient to
       fit the new object, and update the list of reclaimed regions to allow
       splitting regions when necessary. *)
    t.reclaimed <- List.filter_map find_first_fits t.reclaimed;
    t.used_pack_size <- t.used_pack_size ++ Int64.of_int requested_size;
    match !first_fits with
    | None ->
        t.total_pack_size <- t.total_pack_size ++ Int64.of_int requested_size;
        `Append
    | Some offset -> `Offset offset

  let set t v =
    (* Deterministically derive the address of the value. *)
    let h = V.hash v in

    let index = get_current_index t in
    let pack = get_current_pack t in
    let offset = Pack.offset pack in
    let serialized = V.serialize v in
    let size = String.length serialized in

    (* Determine where to store the object in the current pack. *)
    ( match allocate t size with
    | `Offset off -> Pack.set pack ~off serialized
    | `Append ->
        Pack.append pack serialized;
        Index.replace index k (offset, size) );
    Lwt.return_unit

  (** Reclaims the pack storage at [offset..offset+size] used by the object [k]. *)
  let reclaim t k ~offset ~size =
    let index = get_current_index t in
    Index.replace index k (-1L, size);
    t.reclaimed <- (offset, size) :: t.reclaimed;
    t.reclaimed_count <- t.reclaimed_count ++ 1L;
    t.used_pack_size <- t.used_pack_size -- Int64.of_int size

  (** Runs a lazy pass of [filter]. *)
  let lazy_filter t p =
    (* Iterate over all the entries in the current index, and reclaim all those which
       don't need to be kept according to the predicate [p]. *)
    let index = get_current_index t in
    let iter k (offset, size) =
      (* Only reclaim entries which haven't been previously reclaimed (which have an
         offset of -1) and which don't satisfy the predicate [p]. *)
      if offset > -1L && not (p k) then reclaim t k ~offset ~size
    in
    Index.iter iter index;
    Lwt.return_unit

  (** Runs a compaction pass of [filter]. *)
  let compact_filter t p =
    (* Iterate over all the entries in the current index, and copy all the entries
       that need to be kept to the other index and other pack. Once they are all
       copied, atomically switch [t.current], and finally erase the contents of
       the previous index and pack. *)
    let current_index = get_current_index t in
    let other_index = get_other_index t in
    let current_pack = get_current_pack t in
    let other_pack = get_other_pack t in
    let new_pack_size = ref 0L in
    let iter k (old_offset, size) =
      (* Only copy entries which haven't been previously reclaimed (which have an
         offset of -1) and which satisfy the predicate [p]. *)
      if old_offset > -1L && p k then (
        let buffer = Bytes.create size in
        let read = Pack.read current_pack ~off:old_offset buffer in
        if read < size then invalid_arg "No bytes read."
        else
          let new_offset = Pack.offset other_pack in
          Pack.append other_pack (Bytes.unsafe_to_string buffer);
          new_pack_size := !new_pack_size ++ Int64.of_int size;
          Index.replace other_index k (new_offset, size) )
    in
    Index.iter iter current_index;
    t.reclaimed <- [];
    t.reclaimed_count <- 0L;
    t.used_pack_size <- !new_pack_size;
    t.total_pack_size <- !new_pack_size;
    set_current t (match t.current with `A -> `B | `B -> `A);
    Lwt_unix.yield () >>= fun () ->
    Index.clear current_index;
    Pack.clear current_pack;
    Lwt.return_unit

  let filter t p =
    Lwt_mutex.with_lock t.filter_lock (fun () ->
        (* This mutex prevents calls to filter from running concurrently. *)
        if   t.reclaimed_count > reclaimed_threshold
          || t.used_pack_size // t.total_pack_size < loss_ratio_threshold
        then compact_filter t p
        else lazy_filter t p)
end
\end{minted}